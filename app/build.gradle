apply plugin: 'com.android.application'

android {
    compileSdkVersion setup.compileSdkVersion
    buildToolsVersion setup.buildToolsVersion

    defaultConfig {
        applicationId setup.applicationId
        minSdkVersion setup.minSdkVersion
        targetSdkVersion setup.targetSdkVersion
        versionCode buildVersionCode()
        versionName buildVersionName()
        multiDexEnabled true
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            versionNameSuffix setup.devVersionNameSuffix
            applicationIdSuffix setup.devApplicationIdSuffix
            resValue "string", "app_name", String.format("%s", setup.devAppName)
        }
        stage {
            versionNameSuffix setup.stageVersionNameSuffix
            applicationIdSuffix setup.stageApplicationIdSuffix
            resValue "string", "app_name", String.format("%s", setup.stageAppName)
        }
        beta {
            versionNameSuffix setup.betaVersionNameSuffix
            applicationIdSuffix setup.betaApplicationIdSuffix
            resValue "string", "app_name", String.format("%s", setup.betaAppName)
        }
        store {
            resValue "string", "app_name", String.format("%s", setup.appName)
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        pickFirst 'META-INF/LICENSE'
    }
}


dependencies {
    rootProject.appDependencies.each {
        add(it.configuration, it.dependency, it.options)
    }
    api project(':data')
    api project(':domain')
}
